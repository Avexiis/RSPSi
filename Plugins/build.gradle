subprojects {
    group = 'Plugins'
    version = '1.8.0-BETA'

    apply plugin: "com.github.harbby.gradle.serviceloader"

    serviceLoader {
        serviceInterface 'com.rspsi.plugins.core.ClientPlugin'
        serviceInterface 'com.rspsi.plugins.ui.ApplicationPlugin'
    }

    jar {
        archiveBaseName = project.name
    }

    dependencies {
        compileOnly project(':Editor')
    }

    tasks.register('copyJarToBin', Copy) {
        dependsOn tasks.named('jar')
        from(tasks.named('jar').flatMap { it.archiveFile })
        into new File(project(':Editor').projectDir, 'plugins/active')
    }

    tasks.register('buildAndMove') {
        dependsOn tasks.named('compileJava'),
                tasks.named('serviceLoaderBuild'),
                tasks.named('jar'),
                tasks.named('copyJarToBin')
    }

    tasks.named('build') {
        dependsOn tasks.named('copyJarToBin')
    }
}
